## Automa√ß√£o de Coleta de Dados via Web Scraping com Power Query

 #### üìå Coletando dados de Feriados Nacionais diretamente do site da [ANBIMA](https://www.anbima.com.br/feriados/fer_nacionais/2024.asp).

**Objetivo:**
Automatizar a obten√ß√£o de informa√ß√µes sobre feriados nacionais brasileiros a partir do site da ANBIMA, permitindo an√°lise e visualiza√ß√£o dos dados em Power BI.

**Etapa 1: Definindo uma fun√ß√£o para coleta de dados** 

Esta fun√ß√£o √© usada para automatizar a obten√ß√£o de informa√ß√µes sobre feriados nacionais a partir do site da ANBIMA. Ele permite que o usu√°rio especifique o ano de interesse, e o script baixa e processa a p√°gina web correspondente, extraindo a tabela de feriados, promovendo os cabe√ßalhos e alterando os tipos de dados para facilitar an√°lises futuras.
```
(Ano as text) =>

let
    Fonte = Web.BrowserContents("https://www.anbima.com.br/feriados/fer_nacionais/" & Ano & ".asp"),
    #"Tabela extra√≠da de HTML" = Html.Table(Fonte, 
    {
        {"Column1", "TABLE.interna > * > TR > :nth-child(1)"}, 
        {"Column2", "TABLE.interna > * > TR > :nth-child(2)"}, 
        {"Column3", "TABLE.interna > * > TR > :nth-child(3)"}
    }, 
    [RowSelector="TABLE.interna > * > TR"]),
    #"Cabe√ßalhos Promovidos" = Table.PromoteHeaders(#"Tabela extra√≠da de HTML", [PromoteAllScalars=true]),
    #"Tipo Alterado" = Table.TransformColumnTypes(#"Cabe√ßalhos Promovidos",
    {
        {"Data", type date}, 
        {"Dia da Semana", type text},
        {"Feriado", type text}
    })
in
    #"Tipo Alterado"
```
![image](https://github.com/user-attachments/assets/d0dfc9af-77c8-45b9-8e89-87bb98cf6e13)

**Etapa 2: Coleta e Consolida√ß√£o de Dados**

O site da ANBIMA disponibiliza informa√ß√µes sobre os feriados nacionais brasileiros a partir do ano de 2001. O script abaixo automatiza o processo de coleta desses dados, gerando uma tabela consolidada com todos os feriados nacionais de 2001 at√© o ano atual. A fun√ß√£o previamente criada √© utilizada para extrair os dados de cada ano, que s√£o ent√£o agregados em uma √∫nica tabela com todas as informa√ß√µes dos feriados ao longo dos anos. 
```
let
    AnoInicial = 2001,
    AnoFinal = Date.Year(DateTime.LocalNow()),

    // Gera uma lista de n√∫meros do ano de 2001 at√© o ano atual
    ListaAnos = List.Numbers(AnoInicial, AnoFinal - AnoInicial + 1), 
    
    // Converte a lista em tabela com a coluna "Ano"
    Fonte = Table.TransformColumnTypes(Table.FromList(ListaAnos, Splitter.SplitByNothing(), {"Ano"}), {{"Ano", type text}}),
        
    #"Fun√ß√£o Personalizada Invocada" = Table.AddColumn(Fonte, "Feriados", each #"Feriados nacionais Anbima"([Ano])),
    #"Feriados Expandido" = Table.ExpandTableColumn(#"Fun√ß√£o Personalizada Invocada", "Feriados", {"Data", "Dia da Semana", "Feriado"}, {"Data", "Dia da Semana", "Feriado"}),
    #"Changed Type" = Table.TransformColumnTypes(#"Feriados Expandido",{{"Data", type date}, {"Dia da Semana", type text}, {"Feriado", type text}})
in
    #"Changed Type"
```
![image](https://github.com/user-attachments/assets/d1361f79-08fa-4d52-8de6-bdda5807e048)

**Etapa 3: Visualiza√ß√£o dos Dados de Feriados Nacionais**  

Ap√≥s consolidar os dados dos feriados nacionais, podemos criar uma visualiza√ß√£o para entender melhor a distribui√ß√£o dos feriados ao longo do ano. Abaixo est√° um exemplo de visualiza√ß√£o criada no Power BI utilizando os dados coletados:
![image](https://github.com/user-attachments/assets/9d96c29d-8eca-43a9-a7b1-d754c7b741d2)

#### üìå Coletando dados da Compensa√ß√£o Financeira distribu√≠da aos munic√≠pios atrav√©s do site da [ANM](https://sistemas.anm.gov.br/arrecadacao/extra/Relatorios/distribuicao_cfem_muni.aspx?ano=2022&uf=PA).

**Objetivo:**
Automatizar a coleta dos dados de Compensa√ß√£o Financeira pela Explora√ß√£o de Recursos Minerais (CFEM) distribu√≠da aos munic√≠pios, facilitando a an√°lise financeira de receitas municipais ao longo dos anos.

**Etapa 1: Cria√ß√£o de Par√¢metros**

Inicialmente, criamos dois par√¢metros no Power Query para filtrar os dados pela unidade federativa (UF) e pelo munic√≠pio. Esses par√¢metros s√£o utilizados para gerar URLs din√¢micas que permitem acessar as informa√ß√µes espec√≠ficas do munic√≠pio desejado.
```
"PA" meta [IsParameterQuery=true, Type="Text", IsParameterQueryRequired=true]
```
```
"Parauapebas" meta [IsParameterQuery=true, Type="Text", IsParameterQueryRequired=true]
```
![image](https://github.com/user-attachments/assets/f1590ca6-1d19-48b9-9dad-494495e1ba19)

**Etapa 2: Coleta e Consolida√ß√£o de Dados**

O site da ANM disponibiliza informa√ß√µes sobre a compensa√ß√£o financeira dos munic√≠pios a partir de 2004. O script abaixo automatiza o processo de coleta desses dados, gerando uma tabela consolidada com o valor da compensa√ß√£o anual recebido pelo munic√≠pio desde 2004 at√© o √∫ltimo ano fechado. O script utiliza os par√¢metros definidos anteriormente para filtrar os dados pela unidade federativa (UF) e pelo munic√≠pio. Ele gera uma lista de URLs din√¢micas para cada ano, carrega os dados correspondentes e os combina em uma √∫nica tabela com todas as compensa√ß√µes recebidas pelo munic√≠pio ao longo dos anos. 
```
let
    // Cria a lista de anos
    AnoInicial = 2004,
    AnoAtual = Date.Year(DateTime.LocalNow()),
    Anos = List.Transform(List.Numbers(AnoInicial, AnoAtual - AnoInicial), each _),

    // Gera uma lista de URLs din√¢micas
    URLs = List.Transform(Anos, each "https://sistemas.anm.gov.br/arrecadacao/extra/relatorios/distribuicao_cfem_muni.aspx?ano=" & Number.ToText(_) & "&uf=" & Text.Upper(Parameter_SIGLA_UF)),

    // Fun√ß√£o para carregar cada tabela e adicionar coluna do ano
    ObterTabela = (ano as number, url as text) => 
        let
            Fonte = Web.BrowserContents(url),
            #"Extracted Table From Html" = Html.Table(Fonte, {{"Estado", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(1)"}, {"Jan", ":not(*)"}, {"Fev", ":not(*)"}, {"Mar", ":not(*)"}, {"Abri", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(6)"}, {"Jun", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(7)"}, {"Jul", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(8)"}, {"Ago", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(9)"}, {"Set", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(10)"}, {"Out", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(11)"}, {"Nov", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(12)"}, {"Dez", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(13)"}, {"Total", "TABLE.tabelaRelatorio > TBODY > TR > :nth-child(14)"}}, [RowSelector="TABLE.tabelaRelatorio > TBODY > TR"]),
            #"Changed Type" = Table.TransformColumnTypes(#"Extracted Table From Html",{{"Estado", type text}, {"Jan", type text}, {"Fev", type text}, {"Mar", type text}, {"Abri", type number}, {"Jun", type number}, {"Jul", type number}, {"Ago", type number}, {"Set", type number}, {"Out", type number}, {"Nov", type number}, {"Dez", type number}, {"Total", Currency.Type}}),
            #"Filtered Rows" = Table.SelectRows(#"Changed Type", each ([Estado] = Text.Upper(Parameter_MUNICIPIO))),
            #"Removed Columns" = Table.RemoveColumns(#"Filtered Rows",{"Jan", "Fev", "Mar", "Abri", "Jun", "Jul", "Ago", "Set", "Out", "Nov", "Dez"}),
            #"Added Custom" = Table.AddColumn(#"Removed Columns", "Ano", each ano),
            TabelaExtraida = #"Added Custom"
        in
            TabelaExtraida,

    // Carrega e combina as tabelas
    Tabelas = List.Transform(List.Zip({Anos, URLs}), each ObterTabela(_{0}, _{1})),
    TabelaFinal = Table.Combine(Tabelas),
    #"Capitalized Each Word" = Table.TransformColumns(TabelaFinal,{{"Estado", Text.Proper, type text}})
in
    #"Capitalized Each Word"
```
![image](https://github.com/user-attachments/assets/61b39327-a786-4453-9cc6-08d1eb73d4af)

**Etapa 3: Visualiza√ß√£o dos Dados de Compensa√ß√£o Financeira** 

Ap√≥s consolidar os dados de compensa√ß√£o financeira, podemos criar uma visualiza√ß√£o no Power BI para entender melhor a distribui√ß√£o do CFEM ao longo dos anos. Abaixo est√° um exemplo de visualiza√ß√£o criada no Power BI utilizando os dados coletados.
![image](https://github.com/user-attachments/assets/9a47ebd7-50b5-482c-a632-653ddc637376)

